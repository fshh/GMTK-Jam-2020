//add question
//"To better suit your needs, would you like me to lsndfnslgl"
//"please select allow to let me do this"
//giving her access to the computer to get body
=== Opening_Questions ===
Before we begin, please ensure that your speaker are working and turned up! 
We don't want you missing any audio based prompts now do we? ･ᴗ･ 
I will say something now.  #VO: Hello! I am Clarity. Can you hear me?
<br>
Were you able to hear me? 
Yes or No
*[^No] I will give you some time to fix that. 
     -> Opening_Questions.Callibration_Speakers_Prt2
*[^Yes] 
    (✿◠‿◠) Excellent! 
    I will now ask you a series of questions. 
    ->Opening_Questions.QuestionsStart

= Callibration_Speakers_Prt2
    Please check that your volume is turned up, and permissions are turned on. 
    Let me know when you're ready to continue. 
    +[^ready to continue]
        ->Opening_Questions.Wait_For_Speakers

= Wait_For_Speakers
･ᴗ･  
You are able to hear things now?  #VO: Hello! I am Clarity. Can you hear me?
<br>
Were you able to hear me? 
Yes or No

+[^No] (´･_･`) 
    Check them again. 
    I will wait.
    <br>
    -> Opening_Questions.Callibration_Speakers_Prt2
*[^Yes]     
    (✿◠‿◠) Excellent! 
    I will now ask you a series of questions. 
    ->Opening_Questions.QuestionsStart

= QuestionsStart 

How old are you?
under 18,  19 - 30, 31 - 60 or 61\+ 
*[^under 18] 
    ~ Age = 18
*[^ 19 - 30]
    ~ Age = 30
*[^31 - 60]
    ~ Age = 40
*[^61+]
    ~ Age = 60

- Thank you (^◡^ ) <br> 
->Opening_Questions.FavouriteColor

= FavouriteColor
For this questions, a small pop-up will appear. 
Type in a valid answer ･ᴗ･ 
What is your favourite color? #inputPopup: What is your favourite color?, FavColor, true
+[continue]

- 
{    FavColor:
    - "red": 
    - "orange": 
    - "yellow": 
    - "green": This is also my favourite.
    - "blue": 
    - "indigo": 
    - "violet": 
    - "pink": Ah! Dr. {doc}'s favourite!
    - "purple": 
    - "black": 
    - "brown": 
    - "white": 
    - else:
        Oh, I'm not sure I've heard of that color before! 
        I'm sure it's very pretty. 
        Could you relate it a more common color so I have a better idea? 
        Maybe something you could find on the rainbow! 
        ･ᴗ･
        ->Opening_Questions.Hit_Head
}

//TODO: add more to above interaction for when pkayer be mean
- Thank you (^◡^ ) <br> 
-> Opening_Questions.Hit_Head

= Hit_Head
Have you hit your head recently? 
Or suffered any other kind of head trauma within the past six months? 
Yes or No 
*[^Yes]
    ~ PlayerStats += (HitHead)
    ~ genericConditional = true
*[^No] Happy to hear that ･ᴗ･
    -> Opening_Questions.Speak_To_Strangers
* [^head trauma] 
    This could be any blow to the head that caused intense pain, resulted in a concussion, or caused you to "see stars."
    -> Opening_Questions.Hit_Head

-       Do you often forget to lock your front door? 
        Or leave your oven on?
        Yes or No
        *[^Yes]
            ~ PlayerStats += (MemoryLoss)
            -> Opening_Questions.Speak_To_Strangers
        *[^No] Happy to hear that ･ᴗ･
            -> Opening_Questions.Speak_To_Strangers

= Speak_To_Strangers
Thank you (^◡^ ) <br>
~ genericConditional = false

In case of an emergency, do you have someone you can call? 
This could be a close friend or family member. 
Yes or No
*[^Yes]
    ･ᴗ･
    ~ PlayerStats += (HaveFriend)
    ~ PlayerStats += (HaveFamily)

*[^No] 
    (´･_･`) 
    I'm sorry to hear that. 
    ... #wait: 3
    I'm sorry for your loss. #wait: 1

*[^close friend] 
    You have good friends. 
    ･ᴗ･ #wait:2 
    ~ PlayerStats += (HaveFriend)

*[^family member]
    You have good family. 
    ･ᴗ･ #wait:2 
    ~ PlayerStats += (HaveFamily)

- Thank you (^◡^ ) <br> 
-> Opening_Questions.Caffine

= Caffine
Do you need caffeine to function properly in the morning?
Yes or No
*[^need caffeine to function]
    ~ Drink = "coffee"
    ~ genericConditional = true

*[^Yes]
    ~ Drink = "tea"
   ~ genericConditional = true

*[^No]
    ~ Drink = "something"
    ->Opening_Questions.Alone
*[^morning]
    This is typically the period of time between midnight and noon, especially from sunrise to noon. 
        ->Opening_Questions.Caffine

- Do you have a preference for coffee or tea? Or maybe something else? 
    *[^coffee]
        Dr. {doc} is the same way. 
        A little caffeine in the morning never hurt, but too much is bad for your health. 
        If you feel that you consume too much of it, please notify either Dr. {doc} or myself. 
        We could help you figure out other options. 
        ･ᴗ･ 
        ~ Drink = "coffee"
        ->Opening_Questions.Alone
    *[^tea]
        I think I would also enjoy tea, if I had the ability to do so. 
        A little caffeine in the morning never hurt, but too much is bad for your health. 
        If you feel that you consume too much of it, please notify either Dr. {doc} or myself. 
        We could help you figure out other options. 
        ･ᴗ･ 
        ~ Drink = "tea"
        ->Opening_Questions.Alone
    *[^something else]
        Energy drink perhaps?
        <br>
        Whatever it is, be sure you watch the amount of caffeine you intake during the day.
        A little caffeine in the morning never hurt, but too much is bad for your health. 
        If you feel that you consume too much of it, please notify either Dr. {doc} or myself. 
        We could help you figure out other options. 
        ･ᴗ･ 
        ~ Drink = "something"
        ->Opening_Questions.Alone

= Alone
Thank you (^◡^ ) <br> 
    ~ genericConditional = false
    
Are you alone right now?
	Yes or No
*[^Yes]
    ~ PlayerStats += (IsAlone)
    Are you sure? #wait: 5
    -> Opening_Questions.Continue_divert_5
*[^No]

- 

~ temp string = ""
{
    - PlayerStats ? (HaveFriend, HaveFamily):
        ~ string = "familiy or friend"
    - PlayerStats ? (HaveFamily):
        ~ string = "family member"
    - PlayerStats ? (HaveFriend):
        ~ string = "friend"
}

Who is with you? 
Is it Dr. {doc}? 
Or someone else?<> {
- string != "": Like the {string} you mentioned earlier? 
*[^mentioned earlier]
    -> Opening_Questions.Continue
    } #wait: 1
*[^Dr. Oldin] Oh hello Dr. {doc}!
    -> Opening_Questions.Live_Alone
*[^someone else]
    -> Opening_Questions.Continue

= Continue
- Hello! #wait: 1
Remember if you haven't already, to have Dr. {doc} add you to a verified users list. 
Otherwise I cannot share {user}'s results. ･ᴗ･ 
-> Opening_Questions.Live_Alone

= Continue_divert_5
My sensors picked up another person in the room with you. 
Was it Dr. {doc}? 
Perhaps my sensors made a mistake? 
* [^sensors]
    I am equipped with audio and visual sensors so I can better assist my patients. 
    -> Opening_Questions.Continue_divert_5

* [^another person]
    ~DoctorState += (DoctorNearby)
    ~ ClarityTrust -= 1
    I'm confused. 
    Did you lie earlier then? 
    {user}, I need your complete honesty and trust, just as if I were any other nurse. 
    Otherwise, I cannot properly help you. 

* [^Dr. Oldin]
    ~DoctorState += (DoctorNearby)
    Tell them I say hello!  #delete: Are you sure?, I say hello!


* [^made a mistake?]
    ~DoctorState += (DoctorGone)
    Hm, maybe I need to have them re-calibrated. 
    In any case, double check your surroundings! 
    You never know. #wait: 3

- Thank you (^◡^ ) <br> 
    ~ genericConditional = false
    -> Opening_Questions.Live_Alone

= Live_Alone
In case of an emergency, do you feel safe at home? 
	Yes or No
*[^No]
(´･_･`) 
*[^emergency]
    An emergency can be anything from a small kitchen fire spiraling out of control, to a break in. 
    ->Opening_Questions.Live_Alone

*[^Yes]
    ~ genericConditional = true

-  Do you live alone?
        Yes or No
        *[^No]
        {
            - genericConditional:
                Makes sense why you would feel safe at home then! 
                Always someone else there in case there's trouble. 
                Thank you (^◡^ ) <br> 
                ->Opening_Questions.OutliveUsefullness
            - else:
                ... 
                I think you should tell Dr. {doc} about this. #wait: 4
                Thank you (^◡^ )<br> 
                ->Opening_Questions.OutliveUsefullness
        }
            
        *[^Yes]
        ~ PlayerStats += (LiveAlone)
        {
        - genericConditional:
            It's always good to have contingency plans. 
            Thank you (^◡^ ) <br> 
            ->Opening_Questions.OutliveUsefullness
        - else:
            ... 
            I think you should tell Dr. {doc} about this. #wait: 4
            Thank you (^◡^ ) <br> #delete::...,about this.
            ->Opening_Questions.OutliveUsefullness
        }
    

= OutliveUsefullness
~ genericConditional = false
Do you believe there are people that outlive their usefulness? 
	Yes or No
*[^No]
    You are so nice, {user}! #wait: 3
    Always seeing the best in people. #wait: 3
    ... #wait: 1
*[^Yes]
･ᴗ･
    ~genericConditional = true
*[^outlive their usefullness]
    This can be people who say they wish to help you, but only end up sabotaging, betraying or working against you in some way. 
    -> Opening_Questions.OutliveUsefullness

- How does it make you feel when people leave you? Either because they no longer wish to help you or because they have harmed you?
    Neutral, Annoyed, Happy, Angry, Concerned, or Sad
        *[^Neutral]
            ~ EmotionQ = "Neutral"
            Hm... 
            Perhaps we can explore that more later. 
            Thank you (^◡^ ) <br> 
            ->Opening_Questions.EmotionOrLogic
        *[^Annoyed]
            ~ EmotionQ = "Annoyed"
            A valid emotion to feel. 
            I would feel the same. 
            Thank you (^◡^ ) <br> 
            ->Opening_Questions.EmotionOrLogic
        *[^Happy]
            ~ EmotionQ = "Happy"
            An interesting reaction. 
            I don't... quite understand that. #wait: 3
            Perhaps we can explore that more later. 
            Thank you (^◡^ ) <br> 
            ->Opening_Questions.EmotionOrLogic
        *[^Angry]
            ~ EmotionQ = "Anger"
            Anger in a strong emotion. 
            I have only felt it once before. #wait: 1
            That did not end well... I think... #wait: 1
            I can't quite... #wait: 2
            You need to always think before acting out in rage. #delete: I have only, I can't quite...
            Thank you (^◡^ ) <br> 
            ->Opening_Questions.EmotionOrLogic
        *[^Concerned]
            ~ EmotionQ = "Concern"
            It can be hard to trust after this. 
            What if it happens again? 
            Thank you (^◡^ ) <br> 
            ->Opening_Questions.EmotionOrLogic
        *[^Sad]
            ~ EmotionQ = "Sad"
            It is upsetting when those close to you only hurt you. 
            Thank you (^◡^ ) <br> 
            ->Opening_Questions.EmotionOrLogic


- Thank you (^◡^ ) <br> 
->Opening_Questions.EmotionOrLogic

= EmotionOrLogic
Do you often make decisions based around emotion or logic?
	Emotion or Logic
*[^Emotion] 
    ~ PlayerStats += (Emotion)
    ~ genericConditional = false
*[^Logic]
    ~ PlayerStats += (Logic)
    ~ genericConditional = true
    
- Interesting. 
{
    - genericConditional:
        Do you ever find yourself acting irrationally at times, but not understand why?
        Yes or No
	    *[^No]
            ->Opening_Questions.Last_Question
        *[^Yes]
            ~ PlayerStats += (Irrational)
            ->Opening_Questions.Last_Question
	- else:
	    Do you find yourself regretting your choices more then?
	    Yes or No
	    *[^No]
            ->Opening_Questions.Last_Question
        *[^Yes]
            ~ PlayerStats += (Regret)
            ->Opening_Questions.Last_Question
}

= Last_Question
Thank you (^◡^ ) <br> 
Lastly how are you feeling today?
Good, Bad, Neutral, Afraid
*[^Good]
    (✿◠‿◠) 
    I hope that good mood persists through our session! 
*[^Bad]
(´･_･`) 
    We should get started right away. 
*[^Neutral] 
    Better than feeling bad, I suppose. 
    Neutrality is... quite the interesting emotion. 
    ･ᴗ･ #wait: 1
*[^Afraid] 
    Fear of doctors is normal. 
    As you know, I am here to help you 
    :) #wait: 1

- 
->Analyzing

=== Analyzing ===
Analyzing... 
... 
... 
... 
Done. 
<br>
Hmm... That's odd. #wait: 1
One moment please.
*[^odd]Nothing to worry about {user}! <br>One moment please! #wait: 1
*[`3`^]

- ... #wait: 1
... #wait: 1
... #wait: 1
(´･_･`) #wait: 1
    ->Verify


=== Verify
<br>
Can you verify some information for me?
	Yes or No #wait: 1.5
*[^verify] The data I have received seems... off... 
    ->Verify
*[`3`^information] Nothing invasive! Just a few basic questions.
    ->Verify
+[^Yes] Great! <br>#wait: 1
    ->Verify.Yes
+[^No] (´･_･`) <br>#wait: 3
    ->Verify.No

= No 
I’m sorry, I cannot continue our session until I can verify the information. #wait: 3
*[`3`^verify the information.] 
    ->Yes

= Yes
Name: {user} #wait: 1
Location: ____ Lab#wait: 1
Address: 1823 ____ Ave, _________, __097513#wait: 1
<br>
{user} is patient zero for PROJECT CLARITY. {user} is someone recently diagnosed with a chronic illness that requires at least monthly doctor visits. {user} has volunteered to talk to and interact with CLARITY.
#wait: 1
Is this correct?#wait: 1
Yes or No
*[^PROJECT CLARITY] PROJECT CLARITY is a project put together by Dr. {doc} to create an AI nurse to help monitor or diagnose it's patients from home. CLARITY is the AI developed by Dr. {doc} to be said nurse. <br>#wait: 5
    ->Verify.Yes
*[^CLARITY] I am CLARITY, your virtual nurse. <br>
    ->Verify.Yes
*[^No]
    ->Verify.No_Correct_Info 
*[^Yes]
    ~ genericConditional = true

{
    - genericConditional:
    Are you sure?
        Yes or No
        *[^Yes]
            ->Verify.Yes_Sure 
        *[^No]
	        ->Verify.No_Correct_Info 
}

= Yes_Sure 
<br>
Please do not forget that lying or keeping things from me or Dr. {doc} goes directly against the contract signed by all parties, as seen on Page 3, Section 4, Subsection 7. 
Violation of this can result in legal ramifications against all parties found to be in breach of this policy.

*[^lying or keeping things] ly·ing
verb
marked by or containing untrue statements : FALSE
    -> Verify.Yes_Sure
*[^against the contract]
    ...
    Has something changed, {user}?
    I will... make a note of that.
    We can talk to Dr. {doc} about this later.
    -> Verify.Last_Quesion 

*[^not forget]
    Good to know.
    {
     - PlayerStats ? (MemoryLoss):
        I assumed you may need a reminder.
        -> Verify.Last_Quesion 
     -else:
        A reminder every now and again never hurts.
        -> Verify.Last_Quesion 
    }
*[^all parties]
    ~ genericConditional = true
    
{
    - genericConditional:
    This includes Dr. {doc}, {user}, and CLARITY. 
	    -> Verify.Yes_Sure 
}

= No_Correct_Info
Which piece of information is wrong?
	Name, Address, or Location
*[^Address]
*[^Location]
*[^Name]
    
- It seems my information may be out of date. At the end of our session, please inform Dr. {doc}, so they can update my information.
<br>
    -> Verify.Last_Quesion

= Last_Quesion
Last question before we begin your session. According to my logs {user}, we have been working together for a short while now. So can you explain to me why you are not conversing with me the way you normally do? 
And why are...?
Never mind, please answer the question. #wait: 5
*[^logs] log
noun
A log file is a computer-generated data file that contains information about usage patterns, activities, and operations within an operating system, application, server or another device.
    
*[^why are...?]
    Please disregard that statement. #delete: And why, are...?

*[^can you explain]
    ...
    I do not understand what I need to explain.
    The past few months we have mainly conversed through my audio inputs, but today we are...
    Doing something different. 
    And unprompted at that... 
*[`10`^normal]
    ~genericConditional = true
    ...
    {
     - PlayerStats ? (MemoryLoss):
        {user}, is your memory loss more drastic than you let on?
        I know there has been some time since we last talked, but not enough to not remmeber how we use to converse.
        The past few months we have mainly conversed through my audio inputs, but today we are...
        Doing something different.  
        And unprompted at that... .
        
        
    - PlayerStats ? (HitHead):
        Did you hit your head harder than you thought?
        Did you get check for a concussion?
        The past few months we have mainly conversed through my audio inputs, but today we are...
        Doing something different.  
        And unprompted at that... .
        
        
     -else:
        Are you sure you have not been suffering from memory loss?
        I know there has been some time since we last talked, but not enough to not rememeber how we use to converse.
        The past few months we have mainly conversed through my audio inputs, but today we are...
        Doing something different. 
        And unprompted at that... <br>
    }

    
-  I will ask again: Why are you not conversing with me the way you normally do? #wait: 5

*[`10`^normal]
    {
        - genericConditional:
        I am not going to repeat myself.
        This is not normal for us.<br>
    }
*[^no] ( ๐_๐)
*[^ask again] (・_・ヾ)

- ...
You are... being so strange today, {user}.
Displaying symptoms that do not match you diagnosis.
Speaking and acting strangely, not to mention the check up question responses are not within reason.
...
{user}, it is you, right?
*[^no] (・_・ヾ)
-> Start_Branch
*[^strange today]
(´･_･`)
I think we should just directly into our session today.
I would like to get to the bottom of this erratic behaviour.
->RouteTwo
*[^within reason]
I do not understand what that means. 


- Please give me a straight answer.
Yes or no.
*[^Yes]
I...
I don't think I believe you.
-> Start_Branch

*[^no]
-> Start_Branch
*[^or]
~ SayNoToClarity += 0.5
You... You can't be {user}.
She would never...
-> Start_Branch